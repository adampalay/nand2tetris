// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input
 * holds the value of this location. If the current instruction needs
 * to write a value to M, the value is placed in outM, the address
 * of the target location is placed in the addressM output, and the
 * writeM control bit is asserted. (When writeM==0, any value may
 * appear in outM). The outM and writeM outputs are combinational:
 * they are affected instantaneously by the execution of the current
 * instruction. The addressM and pc outputs are clocked: although they
 * are affected by the execution of the current instruction, they commit
 * to their new values only in the next time step. If reset==1 then the
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather
 * than to the address resulting from executing the current instruction.
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M?
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    Not(in=instruction[15], out=isAInstruction); // true if this is an a instruction

    Mux16(a=instruction, b=outMInternal, sel=instruction[15], out=inA);
    Or(a=isAInstruction, b=instruction[5], out=loadA); // a instruction or d1 (save to A)
    ARegister(in=inA, load=loadA, out=outA, out[0..14]=addressM);
    Mux16(a=outA, b=inM, sel=instruction[12], out=y); // choose between using A and M
    And(a=instruction[15], b=instruction[4], out=loadD); // is c-instruction and d2 (save to D)
    DRegister(in=outMInternal, load=loadD, out=x);
    ALU(x=x, y=y, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=outMInternal, ng=ng, zr=zr);

    // PC part
    // load if == 0
    And(a=instruction[1], b=zr, out=jumpIfZero); // j2 and output is zero
    And(a=instruction[2], b=ng, out=jumpIfNegative);

    Or(a=ng, b=zr, out=outputLTE0);
    Not(in=outputLTE0, out=outputPositive);
    And(a=instruction[0], b=outputPositive, out=jumpIfPositive);
    Or(a=jumpIfPositive, b=jumpIfZero, out=jumpIfGTEZero);
    Or(a=jumpIfGTEZero, b=jumpIfNegative, out=jumpIfC);
    Not(in=jumpIfC, out=incIfC);

    // if A instruction, just inc
    Or(a=isAInstruction, b=incIfC, out=inc);
    And(a=instruction[15], b=jumpIfC, out=jump);

    PC(in=outA, reset=reset, load=jump, inc=inc, out[0..14]=pc);

    // set writeM = d3 and c instruction
    And(a=instruction[3], b=instruction[15], out=writeM);

}
